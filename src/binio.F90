MODULE BINIO
  USE PARAMS
  IMPLICIT NONE

  INTEGER, PARAMETER :: IOMSGLEN = 66

CONTAINS

  SUBROUTINE BIO_OPEN(U, FN, ACT, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(INOUT) :: U
    CHARACTER(LEN=*), INTENT(IN) :: FN, ACT
    INTEGER, INTENT(OUT) :: INFO

    CHARACTER(LEN=IOMSGLEN) :: MSG
    CHARACTER(LEN=9) :: FACT
    CHARACTER(LEN=7) :: STAT

    IF (U .LT. -1) THEN
       INFO = -1
    ELSE IF (LEN_TRIM(FN) .LE. 0) THEN
       INFO = -2
    ELSE IF (LEN_TRIM(ACT) .NE. 2) THEN
       INFO = -3
    ELSE
       INFO = 0
    END IF
    IF (INFO .NE. 0) RETURN

    SELECT CASE (ACT(1:1))
    CASE ('R', 'r')
       STAT = 'OLD'
       SELECT CASE (ACT(2:2))
       CASE ('O', 'o')
          FACT = 'READ'
       CASE ('W', 'w')
          FACT = 'READWRITE'
       CASE DEFAULT
          INFO = -3
       END SELECT
    CASE ('W', 'w')
       STAT = 'REPLACE'
       SELECT CASE (ACT(2:2))
       CASE ('O', 'o')
          FACT = 'WRITE'
       CASE ('R', 'r')
          FACT = 'READWRITE'
       CASE DEFAULT
          INFO = -3
       END SELECT
    CASE DEFAULT
       INFO = -3
    END SELECT
    IF (INFO .NE. 0) RETURN

    IF (U .EQ. -1) THEN
       OPEN(NEWUNIT=U, IOSTAT=INFO, IOMSG=MSG, ERR=1, FILE=TRIM(FN), STATUS=TRIM(STAT), ACTION=TRIM(FACT),&
            ACCESS='STREAM', FORM='UNFORMATTED')
    ELSE
       OPEN(UNIT=U, IOSTAT=INFO, IOMSG=MSG, ERR=1, FILE=TRIM(FN), STATUS=TRIM(STAT), ACTION=TRIM(FACT),&
            ACCESS='STREAM', FORM='UNFORMATTED')
    END IF
    RETURN
1   WRITE (ULOG,'(2A)') 'BIO_OPEN    :', TRIM(MSG)
  END SUBROUTINE BIO_OPEN

  SUBROUTINE BIO_CLOSE(U, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(INOUT) :: U
    INTEGER, INTENT(OUT) :: INFO

    CHARACTER(LEN=IOMSGLEN) :: MSG

    INFO = 0
    CLOSE(UNIT=U, IOSTAT=INFO, IOMSG=MSG, ERR=2)
    U = -1
    RETURN
2   WRITE (ULOG,'(2A)') 'BIO_CLOSE   :', TRIM(MSG)
  END SUBROUTINE BIO_CLOSE

  SUBROUTINE BIO_READ_I1(U, M, J, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: U, M
    INTEGER, INTENT(OUT) :: J(M), INFO

    CHARACTER(LEN=IOMSGLEN) :: MSG
    INTEGER :: I

    IF (M .LT. 0) THEN
       INFO = -2
    ELSE
       INFO = 0
    END IF
    IF (INFO .NE. 0) RETURN

    DO I = 1, M
       READ (UNIT=U, IOSTAT=INFO, IOMSG=MSG, ERR=3) J(I)
    END DO
    RETURN
3   WRITE (ULOG,'(2A)') 'BIO_READ_I1 :', TRIM(MSG)
  END SUBROUTINE BIO_READ_I1

  SUBROUTINE BIO_READ_C2(U, M, N, A, LDA, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: U, M, N, LDA
    COMPLEX(KIND=SWP), INTENT(OUT) :: A(LDA,N)
    INTEGER, INTENT(OUT) :: INFO

    CHARACTER(LEN=IOMSGLEN) :: MSG
    INTEGER :: I, J
    COMPLEX(KIND=FWP) :: Z

    IF (M .LT. 0) THEN
       INFO = -2
    ELSE IF (N .LT. 0) THEN
       INFO = -3
    ELSE IF (LDA .LT. M) THEN
       INFO = -5
    ELSE
       INFO = 0
    END IF
    IF (INFO .NE. 0) RETURN

    DO J = 1, N
       DO I = 1, M
          READ (UNIT=U, IOSTAT=INFO, IOMSG=MSG, ERR=4) Z
          A(I,J) = Z
       END DO
    END DO
    RETURN
4   WRITE (ULOG,'(2A)') 'BIO_READ_C2 :', TRIM(MSG)
  END SUBROUTINE BIO_READ_C2

  SUBROUTINE BIO_WRITE_S1(U, N, A, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: U, N
    REAL(KIND=SWP), INTENT(IN) :: A(N)
    INTEGER, INTENT(OUT) :: INFO

    CHARACTER(LEN=IOMSGLEN) :: MSG
    INTEGER :: I
    REAL(KIND=FWP) :: R

    IF (N .LT. 0) THEN
       INFO = -2
    ELSE
       INFO = 0
    END IF
    IF (INFO .NE. 0) RETURN

    DO I = 1, N
       R = REAL(A(I), FWP)
       WRITE (UNIT=U, IOSTAT=INFO, IOMSG=MSG, ERR=5) R
    END DO
    RETURN
5   WRITE (ULOG,'(2A)') 'BIO_WRITE_S1:', TRIM(MSG)
  END SUBROUTINE BIO_WRITE_S1

  SUBROUTINE BIO_WRITE_C2(U, M, N, A, LDA, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: U, M, N, LDA
    COMPLEX(KIND=SWP), INTENT(IN) :: A(LDA,N)
    INTEGER, INTENT(OUT) :: INFO

    CHARACTER(LEN=IOMSGLEN) :: MSG
    INTEGER :: I, J
    COMPLEX(KIND=FWP) :: Z

    IF (M .LT. 0) THEN
       INFO = -2
    ELSE IF (N .LT. 0) THEN
       INFO = -3
    ELSE IF (LDA .LT. M) THEN
       INFO = -5
    ELSE
       INFO = 0
    END IF
    IF (INFO .NE. 0) RETURN

    DO J = 1, N
       DO I = 1, M
          Z = CMPLX(REAL(A(I,J)), AIMAG(A(I,J)), FWP)
          WRITE (UNIT=U, IOSTAT=INFO, IOMSG=MSG, ERR=6) Z
       END DO
    END DO
    RETURN
6   WRITE (ULOG,'(2A)') 'BIO_WRITE_C2:', TRIM(MSG)
  END SUBROUTINE BIO_WRITE_C2

  SUBROUTINE BIO_READ_ALL(FN, M, N, Y, LDY, W, LDW, J, INFO)
    IMPLICIT NONE
    CHARACTER(LEN=*), INTENT(IN) :: FN
    INTEGER, INTENT(IN) :: M, N, LDY, LDW
    COMPLEX(KIND=SWP), INTENT(OUT) :: Y(LDY,N), W(LDW,N)
    INTEGER, INTENT(OUT) :: J(M), INFO

    INTEGER :: U

    IF (LEN_TRIM(FN) .LE. 0) THEN
       INFO = -1
    ELSE IF (M .LT. 0) THEN
       INFO = -2
    ELSE IF (N .LT. 0) THEN
       INFO = -3
    ELSE IF (N .GT. M) THEN
       INFO = -3
    ELSE IF (LDY .LT. M) THEN
       INFO = -5
    ELSE IF (LDW .LT. M) THEN
       INFO = -7
    ELSE
       INFO = 0
    END IF
    IF (INFO .NE. 0) RETURN

    U = -1

    CALL BIO_OPEN(U, TRIM(FN)//'.Y', 'RO', INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_READ_C2(U, M, N, Y, LDY, INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_CLOSE(U, INFO)
    IF (INFO .NE. 0) RETURN

    CALL BIO_OPEN(U, TRIM(FN)//'.W', 'RO', INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_READ_C2(U, M, N, W, LDW, INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_CLOSE(U, INFO)
    IF (INFO .NE. 0) RETURN

    CALL BIO_OPEN(U, TRIM(FN)//'.J', 'RO', INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_READ_I1(U, M, J, INFO)
    CALL BIO_CLOSE(U, INFO)
    IF (INFO .NE. 0) RETURN
  END SUBROUTINE BIO_READ_ALL

  SUBROUTINE BIO_WRITE_ALL(FN, M, N, Y, LDY, W, LDW, Z, LDZ, E, EY, EW, SS, SY, SW, INFO)
    IMPLICIT NONE
    CHARACTER(LEN=*), INTENT(IN) :: FN
    INTEGER, INTENT(IN) :: M, N, LDY, LDW, LDZ
    COMPLEX(KIND=SWP), INTENT(IN) :: Y(LDY,N), W(LDW,N), Z(LDZ,N)
    REAL(KIND=SWP), INTENT(IN) :: E(N), EY(N), EW(N), SS(N), SY(N), SW(N)
    INTEGER, INTENT(OUT) :: INFO

    INTEGER :: U

    IF (LEN_TRIM(FN) .LE. 0) THEN
       INFO = -1
    ELSE IF (M .LT. 0) THEN
       INFO = -2
    ELSE IF (N .LT. 0) THEN
       INFO = -3
    ELSE IF (N .GT. M) THEN
       INFO = -3
    ELSE IF (LDY .LT. M) THEN
       INFO = -5
    ELSE IF (LDW .LT. M) THEN
       INFO = -7
    ELSE IF (LDZ .LT. N) THEN
       INFO = -9
    ELSE
       INFO = 0
    END IF
    IF (INFO .NE. 0) RETURN

    U = -1

    CALL BIO_OPEN(U, TRIM(FN)//'.YU', 'WO', INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_WRITE_C2(U, M, N, Y, LDY, INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_CLOSE(U, INFO)
    IF (INFO .NE. 0) RETURN

    CALL BIO_OPEN(U, TRIM(FN)//'.WV', 'WO', INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_WRITE_C2(U, M, N, W, LDW, INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_CLOSE(U, INFO)
    IF (INFO .NE. 0) RETURN

    CALL BIO_OPEN(U, TRIM(FN)//'.Z', 'WO', INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_WRITE_C2(U, N, N, Z, LDZ, INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_CLOSE(U, INFO)
    IF (INFO .NE. 0) RETURN

    CALL BIO_OPEN(U, TRIM(FN)//'.E', 'WO', INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_WRITE_S1(U, N, E, INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_CLOSE(U, INFO)
    IF (INFO .NE. 0) RETURN

    CALL BIO_OPEN(U, TRIM(FN)//'.EY', 'WO', INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_WRITE_S1(U, N, EY, INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_CLOSE(U, INFO)
    IF (INFO .NE. 0) RETURN

    CALL BIO_OPEN(U, TRIM(FN)//'.EW', 'WO', INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_WRITE_S1(U, N, EW, INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_CLOSE(U, INFO)
    IF (INFO .NE. 0) RETURN

    CALL BIO_OPEN(U, TRIM(FN)//'.SS', 'WO', INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_WRITE_S1(U, N, SS, INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_CLOSE(U, INFO)
    IF (INFO .NE. 0) RETURN

    CALL BIO_OPEN(U, TRIM(FN)//'.SY', 'WO', INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_WRITE_S1(U, N, SY, INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_CLOSE(U, INFO)
    IF (INFO .NE. 0) RETURN

    CALL BIO_OPEN(U, TRIM(FN)//'.SW', 'WO', INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_WRITE_S1(U, N, SW, INFO)
    IF (INFO .NE. 0) RETURN
    CALL BIO_CLOSE(U, INFO)
    IF (INFO .NE. 0) RETURN
  END SUBROUTINE BIO_WRITE_ALL
END MODULE BINIO
